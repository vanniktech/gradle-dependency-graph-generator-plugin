buildscript {
  ext.kotlinVersion = '1.3.21'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    mavenLocal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.16.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.13.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.7.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'com.gradle.plugin-publish'

codeQualityTools {
  ktlint {
    toolVersion = '0.30.0'
  }
  detekt {
    toolVersion = '1.0.0-RC12'
  }
  findbugs {
    enabled = false
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

gradlePlugin {
  plugins {
    graphGeneratorDependencyPlugin {
      id = 'com.vanniktech.dependency.graph.generator'
      implementationClass = 'com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorPlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  implementation localGroovy()
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation gradleApi()
  api "guru.nidi:graphviz-java:0.8.1"

  testImplementation 'junit:junit:4.13-beta-2'
  testImplementation 'org.assertj:assertj-core:3.12.0'
  testImplementation 'com.android.tools.build:gradle:3.3.1'
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    graphGeneratorDependencyPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'graph', 'dependencies', 'graphviz', 'dot']
      description = POM_DESCRIPTION
    }
  }
}

wrapper {
  gradleVersion = '5.2.1'
  distributionType = Wrapper.DistributionType.ALL
}
